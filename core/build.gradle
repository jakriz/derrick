plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'maven'
    id 'signing'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8


dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:3.3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.github.tomakehurst:wiremock:1.57'
    testCompile 'commons-io:commons-io:2.4'

    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'org.apache.commons:commons-lang3:3.4'
}


// jacoco code coverage
check.finalizedBy(jacocoTestReport)

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


// releasing
archivesBaseName = 'derrick'

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
signArchives.enabled = hasProperty("signing.keyId")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : "", password: hasProperty('ossrhPassword') ? ossrhPassword : "")
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : "", password: hasProperty('ossrhPassword') ? ossrhPassword : "")
            }

            pom.project {
                name 'Derrick'
                packaging 'jar'
                description 'Derrick, the documentation inspector, tests your high-level Java documentation and tutorials'
                url 'https://github.com/jakriz/derrick'

                scm {
                    connection 'https://github.com/jakriz/derrick.git'
                    developerConnection 'https://github.com/jakriz/derrick.git'
                    url 'https://github.com/jakriz/derrick'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'jakriz'
                        name 'Jakub Kriz'
                        email 'jacob.kriz@gmail.com'
                    }
                }
            }
        }
    }
}
